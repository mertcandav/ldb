// Copyright 2025 mertcandav.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use "ldb"
use "std/fmt"
use "std/os"

struct User {
	Name: str
	Age:  int
}

async fn main() {
	mut db := ldb::Open("test.db")!

	mut users := db.GetCollection[User]("users").await!
	users.Append(
		{"root", 0},
		{"admin", -1},
		{"Foo", 18},
		{"Bar", 28},
		{"Baz", 93},
		{"Fizz", 53},
		{"Buzz", 36},
		{"ABC", 36},
		{"XYZ", 18}).await!

	mut n := users.Len().await!
	if n != 9 {
		panic("unexpected length")
	}

	t := users.Query().await.
		Where(fn|user| user.Age > 0).
		Sum(fn|user| user.Age)
	fmt::Println("Total of user age: ", t)

	ageRange := users.Query().await.
		Where(fn|user| user.Age > 0).
		GroupBy(fn|user| user.Age).
		Map(fn|g| g.Len())
	fmt::Println("user count per age: ", ageRange)

	mut test := db.GetCollection[User]("test").await!
	test.Append(users.Query().await.Unwrap()...).await!

	users.Close().await
	test.Close().await

	db.DeleteCollection("users").await!
	users = db.GetCollection[User]("users").await!
	n = users.Len().await!
	if n != 0 {
		panic("unexpected length after drop")
	}
	users.Close().await

	db.ClearCollections().await!

	db.Close().await
	os::Remove("test.db")!
}